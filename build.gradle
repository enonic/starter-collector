plugins {
	id 'java' // Needed for assemble and build
	//id 'maven' // When is this needed?
	id 'maven-publish' // Provides publishing and publishToMavenLocal
	id 'com.enonic.xp.app' version '3.4.0'
	//id 'com.moowork.node' version '1.3.1'
	id 'com.github.node-gradle.node' version '7.0.0'
}


repositories {
	mavenLocal()
	xp.enonicRepo('dev')
	mavenCentral()
}


app {
	name = project.appName
	displayName = "${appDisplayName}"
	vendorName = 'Enonic AS'
	vendorUrl = 'http://enonic.com'
	systemVersion = "${xpVersion}"
}


dependencies {
	implementation "com.enonic.xp:core-api:${xpVersion}"
	implementation "com.enonic.xp:portal-api:${xpVersion}"

	//──────────────────────────────────────────────────────────────────────────
	// Core libs (com.enonic.xp)
	//──────────────────────────────────────────────────────────────────────────

	include "com.enonic.xp:lib-admin:${xpVersion}"
	include "com.enonic.xp:lib-auth:${xpVersion}"
	//include "com.enonic.xp:lib-cluster:${xpVersion}"
	//include "com.enonic.xp:lib-common:${xpVersion}"
	//include "com.enonic.xp:lib-content:${xpVersion}"
	include "com.enonic.xp:lib-context:${xpVersion}"
	include "com.enonic.xp:lib-event:${xpVersion}" // Needed by lib-explorer
	//include "com.enonic.xp:lib-i18n:${xpVersion}"
	//include "com.enonic.xp:lib-io:${xpVersion}"
	//include "com.enonic.xp:lib-mail:${xpVersion}"
	//include "com.enonic.xp:lib-mustache:${xpVersion}"
	include "com.enonic.xp:lib-node:${xpVersion}"
	//include "com.enonic.xp:lib-portal:${xpVersion}"
	include "com.enonic.xp:lib-repo:${xpVersion}"
	include "com.enonic.xp:lib-task:${xpVersion}"
	//include "com.enonic.xp:lib-thymeleaf:${xpVersion}"
	//include "com.enonic.xp:lib-websocket:${xpVersion}"
	//include "com.enonic.xp:lib-xslt:${xpVersion}"

	//──────────────────────────────────────────────────────────────────────────
	// Other enonic libs (com.enonic.lib)
	//──────────────────────────────────────────────────────────────────────────

	//include 'com.enonic.lib:lib-cache:1.1.0'
	//include 'com.enonic.lib:lib-guillotine:1.0.1'
	include 'com.enonic.lib:lib-http-client:3.2.2'
	//include 'com.enonic.lib:menu:1.3.3'
	//include 'com.enonic.lib:text-encoding:1.2.0'

	include 'com.enonic.lib:lib-explorer:4.0.0-SNAPSHOT' // version number must be here or dependabot will not upgrade it
	include 'com.enonic.lib:lib-util:3.1.1'

	// WARNING: Do NOT use include files, jar file will be missing dependencies!
}


tasks.withType(Copy) {
  includeEmptyDirs = false
}


//──────────────────────────────────────────────────────────────────────────────
// Gradle wrapper
//──────────────────────────────────────────────────────────────────────────────
wrapper {
	distributionUrl = "${gradleDistributionUrl}"
}


//──────────────────────────────────────────────────────────────────────────────
// Gradle node plugin
//──────────────────────────────────────────────────────────────────────────────
node {
	download = true
	version = "${nodeVersion}"
}


//──────────────────────────────────────────────────────────────────────────────
// Webpack
//──────────────────────────────────────────────────────────────────────────────
sourceSets {
	main {
		java {
			srcDir 'src/main/java'
		}
		resources { // Paths relative to src/main/resources
			exclude 'assets/**/*.svg'
		}
	}
}


task webpack(type:NodeTask) {
	args = [
		'--color'
	]
	dependsOn yarn_install
	environment = [
		'FORCE_COLOR': 'true',
		'LOG_LEVEL_FROM_GRADLE': gradle.startParameter.logLevel.toString(),
		'NODE_ENV': project.hasProperty('dev') || project.hasProperty('development') ? 'development' : 'production'
	]
	mustRunAfter yarn_install
	outputs.dir './build/resources/main' // processResources will delete the directory if this is not present
	script = file('node_modules/webpack-cli/bin/cli.js')
}


task watch(type:NodeTask) {
	script = file('node_modules/webpack-cli/bin/cli.js')
	args = [
		'--color',
		'--watch'
	]
}


processResources {
	dependsOn webpack
	mustRunAfter webpack

	exclude '**/.gitkeep'

	// Let gradle copy these
	//include '**/*.mjs'
	//include '**/*.xml'

	// Let webpack handle these (rather than gradle)
	exclude '**/*.css'
	exclude '**/*.es'
	exclude '**/*.js'
	exclude '**/*.jsx'
	exclude '**/*.less'
	exclude '**/*.sass'
	exclude '**/*.scss'
	exclude '**/*.styl'
	exclude '**/*.ts'
}


publishing {
	publications {
		mavenJava( MavenPublication ) {
			from components.java
		}
	}
	/*repositories {
		maven {
			url 'http://repo.enonic.net/artifactory/public'
		}
	}*/
}
